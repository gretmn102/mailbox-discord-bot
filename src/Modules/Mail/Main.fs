module Mail.Main
open DSharpPlus
open FsharpMyExtension
open FsharpMyExtension.Either

open Types
open Extensions
open Model

type State =
    {
        Mails: Mails.MailDb
        UserEditStates: UserEditStates.GuildData
    }

[<Struct>]
type PostmanType =
    | SantaClaus
    | Valentine
[<CompilationRepresentation(CompilationRepresentationFlags.ModuleSuffix)>]
[<RequireQualifiedAccess>]
module PostmanType =
    let tryDeserialize = function
        | "SantaClaus" -> Ok SantaClaus
        | "Valentine" -> Ok Valentine
        | unknown ->
            let values =
                Reflection.Reflection.unionEnum<PostmanType>
            sprintf "Unknown '%s' postman type. Available values: %A" unknown values
            |> Error

type EditAction =
    | Display
    | To of UserId
    | From of string
    | Description of string
    | ImageUrl of string
    | Return
[<CompilationRepresentation(CompilationRepresentationFlags.ModuleSuffix)>]
[<RequireQualifiedAccess>]
module EditAction =
    module CommandNames =
        let display = "–æ—Ç–æ–±—Ä–∞–∑–∏—Ç—å"
        let ``to`` = "–∫–æ–º—É"
        let from = "–æ—Ç"
        let description = "–æ–ø–∏—Å–∞–Ω–∏–µ"
        let returnToMails = "–≤–µ—Ä–Ω—É—Ç—å—Å—è"
        let image = "–∫–∞—Ä—Ç–∏–Ω–∫–∞"

    let help postmanType =
        match postmanType with
        | SantaClaus ->
            [
                sprintf "–¢—ã –Ω–∞—Ö–æ–¥–∏—à—å—Å—è –≤ —Ä–µ–∂–∏–º–µ —Ä–µ–¥–∞–∫—Ç–∏—Ä–æ–≤–∞–Ω–∏—è –ø–∏—Å—å–º–∞. –î–æ—Å—Ç—É–ø–Ω—ã–µ –∫–æ–º–∞–Ω–¥—ã:"
                sprintf "‚Ä¢ `%s` ‚Äî –æ—Ç–æ–±—Ä–∞–∂–∞–µ—Ç –ø–∏—Å—å–º–æ –≤ —Ç–æ–º –≤–∏–¥–µ, –∫–æ—Ç–æ—Ä–æ–µ –ø–æ–ª—É—á–∏—Ç —É–∫–∞–∑–∞–Ω–Ω—ã–π –ø–æ–ª—å–∑–æ–≤–∞—Ç–µ–ª—å" CommandNames.display
                sprintf "‚Ä¢ `%s <id_–ø–æ–ª—å–∑–æ–≤–∞—Ç–µ–ª—è>` ‚Äî —Å–º–µ–Ω–∏—Ç—å –ø–æ–ª—É—á–∞—Ç–µ–ª—è, –µ—Å–ª–∏ –≤–¥—Ä—É–≥ –æ—à–∏–±–ª–∏—Å—å —Å ID" CommandNames.``to``
                sprintf "‚Ä¢ `%s <–ø—Ä–æ–∏–∑–≤–æ–ª—å–Ω—ã–π_–Ω–∞–±–æ—Ä_—Å–∏–º–≤–æ–ª–æ–≤>` ‚Äî —É–∫–∞–∑—ã–≤–∞–µ—Ç –æ—Ç–ø—Ä–∞–≤–∏—Ç–µ–ª—è. –ü–æ —É–º–æ–ª—á–∞–Ω–∏—é —É–∫–∞–∑–∞–Ω \"–ê–Ω–æ–Ω–∏–º\", –Ω–æ —Ç—ã –º–æ–∂–µ—à—å –ø–æ–¥–ø–∏—Å–∞—Ç—å—Å—è –∫–∞–∫ —É–≥–æ–¥–Ω–æ" CommandNames.from
                sprintf "‚Ä¢ `%s <–ø—Ä–æ–∏–∑–≤–æ–ª—å–Ω—ã–π_–Ω–∞–±–æ—Ä_—Å–∏–º–≤–æ–ª–æ–≤>` ‚Äî —É–∫–∞–∑—ã–≤–∞–µ—Ç —Å–æ–¥–µ—Ä–∂–∏–º–æ–µ —Å–æ–æ–±—â–µ–Ω–∏—è" CommandNames.description
                sprintf "‚Ä¢ `%s [<—Å—Å—ã–ª–∫–∞ –Ω–∞ –∫–∞—Ä—Ç–∏–Ω–∫—É>]` ‚Äî –¥–æ–±–∞–≤–ª—è–µ—Ç –≤–Ω–∏–∑—É –∫–∞—Ä—Ç–∏–Ω–∫—É. –ß—Ç–æ–±—ã —É–±—Ä–∞—Ç—å, –Ω–∞–ø–∏—à–∏—Ç–µ –∫–æ–º–∞–Ω–¥—É –±–µ–∑ –∞—Ä–≥—É–º–µ–Ω—Ç–æ–≤" CommandNames.image
                sprintf "‚Ä¢ `%s` ‚Äî –≤–µ—Ä–Ω—É—Ç—å—Å—è –≤ –≥–ª–∞–≤–Ω–æ–µ –º–µ–Ω—é, —á—Ç–æ–±—ã –Ω–∞–ø–∏—Å–∞—Ç—å –∫—É—á—É –Ω–æ–≤—ã—Ö –ø–æ–∑–¥—Ä–∞–≤–ª–µ–Ω–∏–π, –ª–∏–±–æ –∂–µ –ø–æ–¥—Ä–µ–¥–∞–∫—Ç–∏—Ä–æ–≤–∞—Ç—å —Å—É—â–µ—Å—Ç–≤—É—é—â–∏–µ!" CommandNames.returnToMails
            ]
            |> String.concat "\n"
        | Valentine ->
            [
                sprintf "–¢—ã –Ω–∞—Ö–æ–¥–∏—à—å—Å—è –≤ —Ä–µ–∂–∏–º–µ —Ä–µ–¥–∞–∫—Ç–∏—Ä–æ–≤–∞–Ω–∏—è –≤–∞–ª–µ–Ω—Ç–∏–Ω–∫–∏. –î–æ—Å—Ç—É–ø–Ω—ã–µ –∫–æ–º–∞–Ω–¥—ã:"
                sprintf "‚Ä¢ `%s` ‚Äî –æ—Ç–æ–±—Ä–∞–∂–∞–µ—Ç –≤–∞–ª–µ–Ω—Ç–∏–Ω–∫—É –≤ —Ç–æ–º –≤–∏–¥–µ, –∫–æ—Ç–æ—Ä–æ–µ –ø–æ–ª—É—á–∏—Ç —É–∫–∞–∑–∞–Ω–Ω—ã–π –ø–æ–ª—å–∑–æ–≤–∞—Ç–µ–ª—å" CommandNames.display
                sprintf "‚Ä¢ `%s <id_–ø–æ–ª—å–∑–æ–≤–∞—Ç–µ–ª—è>` ‚Äî —Å–º–µ–Ω–∏—Ç—å –ø–æ–ª—É—á–∞—Ç–µ–ª—è, –µ—Å–ª–∏ –≤–¥—Ä—É–≥ –æ—à–∏–±–ª–∏—Å—å —Å ID" CommandNames.``to``
                sprintf "‚Ä¢ `%s <–ø—Ä–æ–∏–∑–≤–æ–ª—å–Ω—ã–π_–Ω–∞–±–æ—Ä_—Å–∏–º–≤–æ–ª–æ–≤>` ‚Äî —É–∫–∞–∑—ã–≤–∞–µ—Ç –æ—Ç–ø—Ä–∞–≤–∏—Ç–µ–ª—è. –ü–æ —É–º–æ–ª—á–∞–Ω–∏—é —É–∫–∞–∑–∞–Ω \"–ê–Ω–æ–Ω–∏–º\", –Ω–æ —Ç—ã –º–æ–∂–µ—à—å –ø–æ–¥–ø–∏—Å–∞—Ç—å—Å—è –∫–∞–∫ —É–≥–æ–¥–Ω–æ" CommandNames.from
                sprintf "‚Ä¢ `%s <–ø—Ä–æ–∏–∑–≤–æ–ª—å–Ω—ã–π_–Ω–∞–±–æ—Ä_—Å–∏–º–≤–æ–ª–æ–≤>` ‚Äî —É–∫–∞–∑—ã–≤–∞–µ—Ç —Å–æ–¥–µ—Ä–∂–∏–º–æ–µ —Å–æ–æ–±—â–µ–Ω–∏—è" CommandNames.description
                sprintf "‚Ä¢ `%s [<—Å—Å—ã–ª–∫–∞ –Ω–∞ –∫–∞—Ä—Ç–∏–Ω–∫—É>]` ‚Äî –¥–æ–±–∞–≤–ª—è–µ—Ç –≤–Ω–∏–∑—É –∫–∞—Ä—Ç–∏–Ω–∫—É. –ß—Ç–æ–±—ã —É–±—Ä–∞—Ç—å, –Ω–∞–ø–∏—à–∏—Ç–µ –∫–æ–º–∞–Ω–¥—É –±–µ–∑ –∞—Ä–≥—É–º–µ–Ω—Ç–æ–≤" CommandNames.image
                sprintf "‚Ä¢ `%s` ‚Äî –≤–µ—Ä–Ω—É—Ç—å—Å—è –≤ –≥–ª–∞–≤–Ω–æ–µ –º–µ–Ω—é, —á—Ç–æ–±—ã –Ω–∞–ø–∏—Å–∞—Ç—å –∫—É—á—É –Ω–æ–≤—ã—Ö –≤–∞–ª–µ–Ω—Ç–∏–Ω–æ–∫, –ª–∏–±–æ –∂–µ –ø–æ–¥—Ä–µ–¥–∞–∫—Ç–∏—Ä–æ–≤–∞—Ç—å —Å—É—â–µ—Å—Ç–≤—É—é—â–∏–µ!" CommandNames.returnToMails
            ]
            |> String.concat "\n"

    module Parser =
        open FParsec

        open DiscordMessage.Parser

        type 'Result Parser = Primitives.Parser<'Result, unit>

        let pdisplay: _ Parser =
            let p =
                skipStringCI CommandNames.display

            p >>% Display

        let pto: _ Parser =
            let p =
                skipStringCI CommandNames.``to`` .>> spaces
                >>. (puserMention <|> puint64)

            p |>> To

        let pfrom: _ Parser =
            let p =
                skipStringCI CommandNames.from .>> spaces
                >>. many1Satisfy (fun _ -> true)

            p |>> From

        let pdescription: _ Parser =
            let p =
                skipStringCI CommandNames.description .>> spaces
                >>. manySatisfy (fun _ -> true)

            p |>> Description

        let pimage: _ Parser =
            let p =
                skipStringCI CommandNames.image .>> spaces
                >>. manySatisfy (fun _ -> true)

            p |>> ImageUrl

        let preturnToMails: _ Parser =
            let p =
                skipStringCI CommandNames.returnToMails

            p >>% Return

        let start: _ Parser =
            choice [
                pdisplay
                pto
                pfrom
                pdescription
                pimage
                preturnToMails
            ]

    let handle postmanType send displayMail authorId mailId act state =
        let getMailById (mailId: MailId) next =
            match Mails.MailDb.tryFindById mailId state.Mails with
            | Some mail ->
                next mail
            | None ->
                match postmanType with
                | SantaClaus ->
                    sprintf "–ü–∏—Å—å–º–æ —É–¥–∞–ª–∏–ª–æ—Å—å –ø–æ –∫–∞–∫–æ–π-—Ç–æ –ø—Ä–∏—á–∏–Ω–µ. –ù–∞—á–Ω–∏—Ç–µ –∑–∞–Ω–æ–≤–æ."
                | Valentine ->
                    sprintf "–í–∞–ª–µ–Ω—Ç–∏–Ω–∫–∞ —É–¥–∞–ª–∏–ª–∞—Å—å –ø–æ –∫–∞–∫–æ–π-—Ç–æ –ø—Ä–∏—á–∏–Ω–µ. –ù–∞—á–Ω–∏—Ç–µ –∑–∞–Ω–æ–≤–æ."
                |> send

                state

        getMailById mailId <| fun mail ->

        let update updating =
            let newData =
                updating mail.Data

            let mails =
                state.Mails
                |> Mails.MailDb.set
                    mail.Id
                    (fun _ ->
                        newData
                    )

            displayMail { mail with Data = newData }

            { state with
                Mails = mails
            }

        match act with
        | Display ->
            displayMail mail
            state
        | To ``to`` ->
            update (fun mailData ->
                { mailData with
                    Recipient = ``to``
                }
            )
        | From from ->
            update (fun mailData ->
                { mailData with
                    From = from
                }
            )
        | Description description ->
            update (fun mailData ->
                { mailData with
                    Description = description
                }
            )
        | ImageUrl imageUrl ->
            update (fun mailData ->
                { mailData with
                    ImageUrl = imageUrl
                }
            )
        | Return ->
            let userEditStates =
                state.UserEditStates
                |> UserEditStates.GuildData.set
                    authorId
                    (fun x ->
                        { x with
                            Editing = None
                        }
                    )

            help postmanType
            |> send

            { state with
                UserEditStates = userEditStates
            }

type MainAction =
    | CreateMail of UserId
    | DisplayMails
    | EditMail of MailIndex
    | RemoveMail of MailIndex
[<CompilationRepresentation(CompilationRepresentationFlags.ModuleSuffix)>]
[<RequireQualifiedAccess>]
module MainAction =
    module CommandNames =
        let displayMails (postmanType: PostmanType) =
            match postmanType with
            | SantaClaus -> "–ø–∏—Å—å–º–∞"
            | Valentine -> "–≤–∞–ª–µ–Ω—Ç–∏–Ω–∫–∏"
        let createMail = "—Å–æ–∑–¥–∞—Ç—å"
        let editMail = "—Ä–µ–¥–∞–∫—Ç–∏—Ä–æ–≤–∞—Ç—å"
        let removeMail = "—É–¥–∞–ª–∏—Ç—å"

    let help postmanType =
        let displayMailCommandName = CommandNames.displayMails postmanType

        match postmanType with
        | SantaClaus ->
            [
                "–î–æ—Å—Ç—É–ø–Ω—ã–µ –∫–æ–º–∞–Ω–¥—ã:"
                sprintf "‚Ä¢ `%s <id_–ø–æ–ª—å–∑–æ–≤–∞—Ç–µ–ª—è>` ‚Äî –Ω–∞—á–∞—Ç—å –ø–∏—Å–∞—Ç—å –ø–æ–∑–¥—Ä–∞–≤–ª–µ–Ω–∏–µ —É–∫–∞–∑–∞–Ω–Ω–æ–º—É –ø–æ–ª—å–∑–æ–≤–∞—Ç–µ–ª—é;" CommandNames.createMail
                sprintf "‚Ä¢ `%s` ‚Äî –æ—Ç–æ–±—Ä–∞–∂–∞–µ—Ç —Å–ø–∏—Å–æ–∫ –Ω–∞–ø–∏—Å–∞–Ω–Ω—ã—Ö –ø–∏—Å–µ–º;" displayMailCommandName
                sprintf "‚Ä¢ `%s <–∏–Ω–¥–µ–∫—Å_–ø–∏—Å—å–º–∞>` ‚Äî –Ω–∞—á–∞—Ç—å —Ä–µ–¥–∞–∫—Ç–∏—Ä–æ–≤–∞—Ç—å –ø–∏—Å—å–º–æ —Å —É–∫–∞–∑–∞–Ω–Ω—ã–º –∏–Ω–¥–µ–∫—Å–æ–º. –ü–æ—Å–º–æ—Ç—Ä–µ—Ç—å –∏–Ω–¥–µ–∫—Å—ã –ø–∏—Å–µ–º –º–æ–∂–Ω–æ –∫–æ–º–∞–Ω–¥–æ–π `%s` –≤ –≥—Ä–∞—Ñ–µ `‚Ññ`" CommandNames.editMail displayMailCommandName
                sprintf "‚Ä¢ `%s <–∏–Ω–¥–µ–∫—Å_–ø–∏—Å—å–º–∞>` ‚Äî —É–¥–∞–ª–∏—Ç—å –ø–∏—Å—å–º–æ —Å —É–∫–∞–∑–∞–Ω–Ω—ã–º –∏–Ω–¥–µ–∫—Å–æ–º. –ü–æ—Å–º–æ—Ç—Ä–µ—Ç—å –∏–Ω–¥–µ–∫—Å—ã –ø–∏—Å–µ–º –º–æ–∂–Ω–æ –∫–æ–º–∞–Ω–¥–æ–π `%s` –≤ –≥—Ä–∞—Ñ–µ `‚Ññ`" CommandNames.removeMail displayMailCommandName
            ]
            |> String.concat "\n"
        | Valentine ->
            [
                "–î–æ—Å—Ç—É–ø–Ω—ã–µ –∫–æ–º–∞–Ω–¥—ã:"
                sprintf "‚Ä¢ `%s <id_–ø–æ–ª—å–∑–æ–≤–∞—Ç–µ–ª—è>` ‚Äî –Ω–∞—á–∞—Ç—å –ø–∏—Å–∞—Ç—å –≤–∞–ª–µ–Ω—Ç–∏–Ω–∫—É —É–∫–∞–∑–∞–Ω–Ω–æ–º—É –ø–æ–ª—å–∑–æ–≤–∞—Ç–µ–ª—é;" CommandNames.createMail
                sprintf "‚Ä¢ `%s` ‚Äî –æ—Ç–æ–±—Ä–∞–∂–∞–µ—Ç —Å–ø–∏—Å–æ–∫ –Ω–∞–ø–∏—Å–∞–Ω–Ω—ã—Ö –≤–∞–ª–µ–Ω—Ç–∏–Ω–æ–∫;" displayMailCommandName
                sprintf "‚Ä¢ `%s <–∏–Ω–¥–µ–∫—Å_–≤–∞–ª–µ–Ω—Ç–∏–Ω–∫–∏>` ‚Äî –Ω–∞—á–∞—Ç—å —Ä–µ–¥–∞–∫—Ç–∏—Ä–æ–≤–∞—Ç—å –≤–∞–ª–µ–Ω—Ç–∏–Ω–∫—É —Å —É–∫–∞–∑–∞–Ω–Ω—ã–º –∏–Ω–¥–µ–∫—Å–æ–º. –ü–æ—Å–º–æ—Ç—Ä–µ—Ç—å –∏–Ω–¥–µ–∫—Å—ã –≤–∞–ª–µ–Ω—Ç–∏–Ω–æ–∫ –º–æ–∂–Ω–æ –∫–æ–º–∞–Ω–¥–æ–π `%s` –≤ –≥—Ä–∞—Ñ–µ `‚Ññ`" CommandNames.editMail displayMailCommandName
                sprintf "‚Ä¢ `%s <–∏–Ω–¥–µ–∫—Å_–≤–∞–ª–µ–Ω—Ç–∏–Ω–∫–∏>` ‚Äî —É–¥–∞–ª–∏—Ç—å –≤–∞–ª–µ–Ω—Ç–∏–Ω–∫—É —Å —É–∫–∞–∑–∞–Ω–Ω—ã–º –∏–Ω–¥–µ–∫—Å–æ–º. –ü–æ—Å–º–æ—Ç—Ä–µ—Ç—å –∏–Ω–¥–µ–∫—Å—ã –≤–∞–ª–µ–Ω—Ç–∏–Ω–æ–∫ –º–æ–∂–Ω–æ –∫–æ–º–∞–Ω–¥–æ–π `%s` –≤ –≥—Ä–∞—Ñ–µ `‚Ññ`" CommandNames.removeMail displayMailCommandName
            ]
            |> String.concat "\n"

    module Parser =
        open FParsec

        open DiscordMessage.Parser

        type 'Result Parser = Primitives.Parser<'Result, unit>


        let pdisplayMails (postmanType: PostmanType): _ Parser =
            let p =
                skipStringCI (CommandNames.displayMails postmanType)

            p >>% DisplayMails

        let pcreateMail: _ Parser =
            let p =
                skipStringCI CommandNames.createMail .>> spaces
                >>. (puserMention <|> puint64)

            p |>> CreateMail

        let peditMail: _ Parser =
            let p =
                skipStringCI CommandNames.editMail .>> spaces
                >>. pint32

            p |>> EditMail

        let premoveMail: _ Parser =
            let p =
                skipStringCI CommandNames.removeMail .>> spaces
                >>. pint32

            p |>> RemoveMail

        let start (postmanType: PostmanType): _ Parser =
            choice [
                pdisplayMails postmanType
                pcreateMail
                peditMail
                premoveMail
            ]

type Action =
    | EditAction of EditAction * MailId
    | MainAction of MainAction

type Msg =
    | Request of DiscordClient * EventArgs.MessageCreateEventArgs

let reduce (postmanType: PostmanType) (msg: Msg) (state: State): State =
    match msg with
    | Request(client, e) ->
        let send msg =
            let embed = Entities.DiscordEmbedBuilder()
            embed.Color <- Entities.Optional.FromValue(DiscordEmbed.backgroundColorDarkTheme)
            embed.Description <- msg

            let b = Entities.DiscordMessageBuilder()
            b.Embed <- embed.Build()

            awaiti <| e.Channel.SendMessageAsync b

        let checkIsPrivate next =
            let currentUserId = client.CurrentUser.Id

            if e.Author.Id = currentUserId then
                state
            else
                if e.Channel.IsPrivate then
                    next ()
                else
                    if e.Message.MentionedUsers |> Seq.exists (fun user -> user.Id = currentUserId) then
                        sprintf "–ù–∞–ø–∏—à–∏ –º–Ω–µ –≤ –õ–° –ª—é–±–æ–µ —Å–æ–æ–±—â–µ–Ω–∏–µ."
                        |> send

                    state

        let getUserEditState next =
            UserEditStates.GuildData.tryFindById e.Author.Id state.UserEditStates
            |> next

        let checkFirstTime (userStateOpt: UserEditStates.Data option) next =
            match userStateOpt with
            | None ->
                let userEditStates =
                    state.UserEditStates
                    |> UserEditStates.GuildData.set
                        e.Author.Id
                        id

                let msg =
                    match postmanType with
                    | SantaClaus ->
                        [
                            "–•–æ-—Ö–æ-—Ö–æ! –Ø ‚Äî –±–æ—Ç, —Ä–∞—Å—Å—ã–ª–∞—é—â–∏–π –ø–æ–∑–¥—Ä–∞–≤–ª–µ–Ω–∏—è –ª—é–¥—è–º –ø–æ–¥ –ù–æ–≤—ã–π –≥–æ–¥! <:satan:1044024010113032212>"
                            sprintf "–í–≤–µ–¥–∏ `%s <id_–ø–æ–ª—å–∑–æ–≤–∞—Ç–µ–ª—è>`, —á—Ç–æ–±—ã –Ω–∞—á–∞—Ç—å –ø–∏—Å–∞—Ç—å –ø–æ–∑–¥—Ä–∞–≤–ª–µ–Ω–∏–µ —É–∫–∞–∑–∞–Ω–Ω–æ–º—É –ø–æ–ª—å–∑–æ–≤–∞—Ç–µ–ª—é. –ù–∞–ø—Ä–∏–º–µ—Ä:" MainAction.CommandNames.createMail
                            "```"
                            sprintf "%s %d" MainAction.CommandNames.createMail client.CurrentUser.Id
                            "```"
                            "–ù–∞—á–Ω–µ—à—å –ø–∏—Å–∞—Ç—å –ø–∏—Å—å–º–æ –º–Ω–µ, –Ω–æ —è –≤—Å–µ —Ä–∞–≤–Ω–æ –µ–≥–æ –Ω–µ –æ—Ü–µ–Ω—é, –ø–æ—ç—Ç–æ–º—É –ª—É—á—à–µ –Ω–∞–ø—Ä–∞–≤–∏—Ç—å —Å–≤–æ–∏ —Å–∏–ª—ã –Ω–∞ –∫–æ–≥–æ-—Ç–æ –¥—Ä—É–≥–æ–≥–æ. –ü–æ—Å–ª–µ —Ç–æ–≥–æ, –∫–∞–∫ –Ω–∞–ø–∏—à–µ—à—å, —Å–ª–µ–¥—É–π –º–æ–∏–º —É–∫–∞–∑–∞–Ω–∏—è–º."
                        ]
                        |> String.concat "\n"
                    | Valentine ->
                        [
                            "–ü-–ø—Ä–∏–≤–µ—Ç! –°–µ–≥–æ–¥–Ω—è –º–æ–π –ø–µ—Ä–≤—ã–π —Ä–∞–±–æ—á–∏–π –¥–µ–Ω—å, –∏ —è –Ω–µ–º–Ω–æ–≥–æ –≤–æ–ª–Ω—É—é—Å—å, –ø–æ—ç—Ç–æ–º—É... –ß—Ç–æ, –∫–∞–∫–æ–π –∑–∞–ø–∞—Ö –≤–∞–ª–µ—Ä—å—è–Ω–∫–∏?! –≠—Ç–æ –æ–¥–µ–∫–æ–ª–æ–Ω! :scream_cat:"
                            "–õ–∞–¥–Ω–æ, –æ —á–µ–º —ç—Ç–æ —è? –ê—Ö, –¥–∞: –¥–µ–Ω—å –≤–ª—é–±–ª–µ–Ω–Ω—ã—Ö! –ü–æ—Ä–∞ —Ä–æ–º–∞–Ω—Ç–∏–∫–∏, –≥—Ä–µ–∑ –∏... –≤–∞–ª–µ–Ω—Ç–∏–Ω–æ–∫. –Ø –∫–∞–∫ —Ä–∞–∑ —Ç–∞–∫–∏–µ —Å–æ–±–∏—Ä–∞—é –∏ –¥–æ—Å—Ç–∞–≤–ª—è—é –¥—Ä—É–≥–∏–º –ø–æ–ª–æ–≤–∏–Ω–∫–∞–º."
                            sprintf "–í–≤–µ–¥–∏ `%s <id_–ø–æ–ª—å–∑–æ–≤–∞—Ç–µ–ª—è>`, —á—Ç–æ–±—ã –Ω–∞—á–∞—Ç—å –ø–∏—Å–∞—Ç—å –≤–∞–ª–µ–Ω—Ç–∏–Ω–∫—É —É–∫–∞–∑–∞–Ω–Ω–æ–º—É –ø–æ–ª—å–∑–æ–≤–∞—Ç–µ–ª—é." MainAction.CommandNames.createMail
                            ""
                            "–ù–∞–ø—Ä–∏–º–µ—Ä, –µ—Å–ª–∏ –Ω–∞–ø–∏—Å–∞—Ç—å —Ç–∞–∫:"
                            "```"
                            sprintf "%s %d" MainAction.CommandNames.createMail client.CurrentUser.Id
                            "```"
                            "–¢–æ —ç—Ç–∞ –≤–∞–ª–µ–Ω—Ç–∏–∫–∞ –ø—Ä–∏–¥–µ—Ç –∫–æ –º-–º–Ω–µ, –≤–æ—Ç—å."
                            "*–ù–µ–ª–æ–≤–∫–æ –º–Ω–µ—Ç—Å—è*"
                            ""
                            "–ü–æ—Å–ª–µ —Ç–æ–≥–æ, –∫–∞–∫ –Ω–∞–ø–∏—à–µ—à—å, —Å–ª–µ–¥—É–π –¥–∞–ª—å–Ω–µ–π—à–∏–º —É–∫–∞–∑–∞–Ω–∏—è–º, –∏ –≤—Å—ë –±—É–¥–µ—Ç —Ö–æ—Ä–æ—à–æ üê±"
                        ]
                        |> String.concat "\n"
                send msg

                { state with
                    UserEditStates = userEditStates
                }

            | Some userState ->
                next userState

        let parseCommandByUserState (userState: UserEditStates.Data) next =
            match userState.Data.Editing with
            | Some mailId ->
                match FParsecExt.runResult EditAction.Parser.start e.Message.Content with
                | Ok act ->
                    next (EditAction(act, mailId))
                | Error msg ->
                    EditAction.help postmanType
                    |> send

                    state
            | None ->
                match FParsecExt.runResult (MainAction.Parser.start postmanType) e.Message.Content with
                | Ok act ->
                    next (MainAction act)
                | Error msg ->
                    MainAction.help postmanType
                    |> send

                    state

        checkIsPrivate <| fun () ->
        getUserEditState <| fun userStateOpt ->
        checkFirstTime userStateOpt <| fun userState ->
        parseCommandByUserState userState <| fun command ->

        let displayMail (mail: Mails.Mail) =
            let embed = Entities.DiscordEmbedBuilder()

            embed.Color <- Entities.Optional.FromValue(DiscordEmbed.backgroundColorDarkTheme)
            embed.Description <- mail.Data.Description
            embed.WithAuthor(mail.Data.From) |> ignore

            let imageUrl = mail.Data.ImageUrl
            if not <| System.String.IsNullOrEmpty imageUrl then
                embed.ImageUrl <- imageUrl

            let b = Entities.DiscordMessageBuilder()
            b.Content <-
                let recipientId = mail.Data.Recipient
                sprintf "<@%d>(%d) –ø–æ–ª—É—á–∏—Ç —Ç–∞–∫–æ–µ —Å–æ–æ–±—â–µ–Ω–∏–µ:" recipientId recipientId
            b.Embed <- embed.Build()

            awaiti <| e.Channel.SendMessageAsync b

        match command with
        | EditAction(act, mailId) ->
            EditAction.handle postmanType send displayMail e.Author.Id mailId act state

        | MainAction(act) ->
            let displayMails (mails: Mails.MailDb) =
                let getMails next =
                    match Mails.MailDb.tryFindByUserId e.Author.Id mails with
                    | Some mails ->
                        next mails
                    | None ->
                        match postmanType with
                        | SantaClaus ->
                            "–°–ø–∏—Å–æ–∫ –ø–∏—Å–µ–º –ø—É—Å—Ç."
                        | Valentine ->
                            "–°–ø–∏—Å–æ–∫ –≤–∞–ª–µ–Ω—Ç–∏–Ω–æ–∫ –ø—É—Å—Ç."
                        |> send

                getMails <| fun mails ->

                [
                    "‚Ññ | –∫–æ–º—É | –æ–ø–∏—Å–∞–Ω–∏–µ"
                    yield! mails
                    |> List.mapi (fun i mailId ->
                        match Mails.MailDb.tryFindById mailId state.Mails with
                        | Some mail ->
                            let description =
                                let maxLength = 64
                                let description =
                                    mail.Data.Description
                                    |> String.replace "\n" " "
                                if description.Length < maxLength then
                                    description
                                else
                                    description.[0..maxLength - 1]

                            sprintf "%d | <@%d> | %s" i mail.Data.Recipient description
                        | None ->
                            sprintf "%d %s" i mailId
                    )
                ]
                |> String.concat "\n"
                |> send

            let getMailByIndex (mailIndex: MailIndex) next =
                match Mails.MailDb.tryFindByMailIndex e.Author.Id mailIndex state.Mails with
                | Some mail ->
                    next mail
                | None ->
                    let displayMailCommandName = MainAction.CommandNames.displayMails postmanType

                    match postmanType with
                    | SantaClaus ->
                        sprintf "–ü–∏—Å—å–º–æ –ø–æ–¥ –Ω–æ–º–µ—Ä–æ–º %d –Ω–µ –Ω–∞–π–¥–µ–Ω–æ. –ü–æ–ø—Ä–æ–±—É–π—Ç–µ –µ—â–µ —Ä–∞–∑ –≤—ã–∑–≤–∞—Ç—å —Å–ø–∏—Å–æ–∫ –ø–∏—Å–µ–º –∫–æ–º–∞–Ω–¥–æ–π `%s` –∏ —É–∫–∞–∑–∞—Ç—å –Ω–æ–º–µ—Ä –Ω—É–∂–Ω–æ–≥–æ –ø–∏—Å—å–º–∞ –¥–ª—è —Ä–µ–¥–∞–∫—Ç–∏—Ä–æ–≤–∞–Ω–∏—è."
                            mailIndex
                            displayMailCommandName
                    | Valentine ->
                        sprintf "–í–∞–ª–µ–Ω—Ç–∏–Ω–∫–∞ –ø–æ–¥ –Ω–æ–º–µ—Ä–æ–º %d –Ω–µ –Ω–∞–π–¥–µ–Ω–æ. –ü–æ–ø—Ä–æ–±—É–π—Ç–µ –µ—â–µ —Ä–∞–∑ –≤—ã–∑–≤–∞—Ç—å —Å–ø–∏—Å–æ–∫ –ø–∏—Å–µ–º –∫–æ–º–∞–Ω–¥–æ–π `%s` –∏ —É–∫–∞–∑–∞—Ç—å –Ω–æ–º–µ—Ä –Ω—É–∂–Ω–æ–≥–æ –ø–∏—Å—å–º–∞ –¥–ª—è —Ä–µ–¥–∞–∫—Ç–∏—Ä–æ–≤–∞–Ω–∏—è."
                            mailIndex
                            displayMailCommandName
                    |> send

                    state

            match act with
            | EditMail mailIndex ->
                getMailByIndex mailIndex <| fun mail ->

                let userEditStates =
                    state.UserEditStates
                    |> UserEditStates.GuildData.set
                        e.Author.Id
                        (fun x ->
                            { x with
                                Editing = Some mail.Id
                            }
                        )

                EditAction.help postmanType
                |> send

                displayMail mail

                { state with
                    UserEditStates = userEditStates
                }

            | RemoveMail mailIndex ->
                getMailByIndex mailIndex <| fun mail ->

                match Mails.MailDb.removeByMail mail state.Mails with
                | Some mails ->
                    match postmanType with
                    | SantaClaus ->
                        sprintf "–ü–æ–∑–¥—Ä–∞–≤–ª–µ–Ω–∏–µ ‚Ññ %d —É–¥–∞–ª–µ–Ω–æ." mailIndex
                    | Valentine ->
                        sprintf "–í–∞–ª–µ–Ω—Ç–∏–Ω–∫–∞ ‚Ññ %d —É–¥–∞–ª–µ–Ω–∞." mailIndex
                    |> send

                    displayMails mails

                    { state with
                        Mails = mails
                    }
                | None ->
                    match postmanType with
                    | SantaClaus ->
                        sprintf "–ü–æ–∑–¥—Ä–∞–≤–ª–µ–Ω–∏–µ ‚Ññ %d –Ω–µ —É–¥–∞–ª–æ—Å—å —É–¥–∞–ª–∏—Ç—å. –ü–æ–≤—Ç–æ—Ä–∏—Ç–µ –µ—â–µ —Ä–∞–∑." mailIndex
                    | Valentine ->
                        sprintf "–í–∞–ª–µ–Ω—Ç–∏–Ω–∫—É ‚Ññ %d –Ω–µ —É–¥–∞–ª–æ—Å—å —É–¥–∞–ª–∏—Ç—å. –ü–æ–≤—Ç–æ—Ä–∏—Ç–µ –µ—â–µ —Ä–∞–∑." mailIndex
                    |> send

                    displayMails state.Mails

                    state

            | CreateMail recipientId ->
                let newMailId =
                    MailId.generateNew ()

                let mailData =
                    { Mails.MailData.Empty with
                        Author = e.Author.Id
                        Recipient = recipientId
                    }

                let mails =
                    state.Mails
                    |> Mails.MailDb.set
                        newMailId
                        (fun _ ->
                            mailData
                        )

                let userEditStates =
                    state.UserEditStates
                    |> UserEditStates.GuildData.set
                        e.Author.Id
                        (fun x ->
                            { x with
                                Editing = Some newMailId
                            }
                        )

                let mail =
                    Mails.Mail.create
                        newMailId
                        mailData

                EditAction.help postmanType
                |> send

                displayMail mail

                { state with
                    Mails = mails
                    UserEditStates = userEditStates
                }

            | DisplayMails ->
                displayMails state.Mails

                state

let create (postmanType: PostmanType) db =
    let m =
        let init: State = {
            Mails = Mails.MailDb.init "mailboxUsers" db
            UserEditStates = UserEditStates.GuildData.init "mailboxUserEditStates" db
        }

        MailboxProcessor.Start (fun mail ->
            let rec loop (state: State) =
                async {
                    let! msg = mail.Receive()
                    let state =
                        try
                            reduce postmanType msg state
                        with e ->
                            printfn "%A" e
                            state

                    return! loop state
                }
            loop init
        )

    { Shared.BotModule.empty with
        MessageCreateEventHandle =
            let exec (e, msg) =
                m.Post (Request (e, msg))
            Some exec
    }
